plugins {
    id 'com.android.library'
    id 'com.google.protobuf'
    id 'kotlin-android'
    id 'kotlin-parcelize'

}
android {
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        buildConfigField "String", "APPLICATION_ID", "\"com.fundot.p4bu\""

    }
    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            lintOptions {
                abortOnError false
            }
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // The flavor dimensions for build variants (e.g. aospWithQuickstep, aospWithoutQuickstep)
    // See: https://developer.android.com/studio/build/build-variants#flavor-dimensions
    flavorDimensions "app", "recents"

    productFlavors {
        aosp {
            dimension "app"
//            applicationId 'com.android.ttlauncher3'
            testApplicationId 'com.android.launcher3.tests'
        }


        withoutQuickstep {
            dimension "recents"
        }
    }

    sourceSets {
        main {
            res.srcDirs = ['res']
            java.srcDirs = ['src']
            manifest.srcFile 'AndroidManifest-common.xml'
            proto {
                srcDirs = ['protos/', 'protos_overrides/']
            }
        }

        aosp {
            java.srcDirs = ['src_flags', 'src_shortcuts_overrides']
        }
        withoutQuickstep {
            java.srcDirs = ['src_ui_overrides']
        }
        aospWithoutQuickstep {
            manifest.srcFile "AndroidManifest.xml"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'lib', include: ['*.jar'])

    implementation "androidx.dynamicanimation:dynamicanimation:1.0.0"
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "androidx.preference:preference:1.2.0"
    implementation "androidx.slice:slice-builders:1.0.0"
    implementation project(':Iconloaderlib')
    // Required for AOSP to compile. This is already included in the sysui_shared.jar
    api fileTree(dir: "libs", include: 'plugin_core.jar')

    implementation 'com.google.protobuf:protobuf-javalite:3.8.0'
    implementation 'com.google.android.material:material:1.5.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation "org.mockito:mockito-core:4.0.0"
    androidTestImplementation 'com.google.dexmaker:dexmaker:1.2'
    androidTestImplementation 'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'
    androidTestImplementation "androidx.annotation:annotation:1.4.0"
}
// https://github.com/google/protobuf-gradle-plugin
protobuf {
    // Configure the protoc executable
    protoc {
        artifact = "com.google.protobuf:protoc:3.8.0"
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
                java {
                    option "lite"
                }
            }
        }
    }
}
